apply plugin: 'com.android.application'

android {
    // !!! IMPORTANT !!!
    // When changing any of the following: compileSdkVersion, targetSdkVersion
    // don't forget to also update travis.yml config
    // !!! IMPORTANT !!!

    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28

        /**
         * ------------------------------------------------------------
         *
         *              CHANGE PER RELEASE BUILD
         *              SEE docs/release.txt FOR INFO
         *
         * ------------------------------------------------------------
         */
        //bump the MAJOR (XX) version when there are incompatible changes between versions
        //or major changes to the application that would require a reinstall
        //bump the MINOR (YY) version when there are compatible changes between versions (ie database changes)
        //or if there are big application level changes like tearing out/adding major features
        //bump the PATCH (ZZ) version otherwise, for anything small
        int major = 4
        int minor = 7
        int patch = 1

        /**
         * ------------------------------------------------------------
         *
         *              CHANGE PER NEW FORK MODIFICATION
         *              IF AND ONLY IF NECESSARY
         *
         * ------------------------------------------------------------
         */
        //if you change this, also change the AndroidManifest package
        applicationId "com.github.adamantcheese.chan"
        //these are your update and github repo endpoints, change it to your repository
        //the repo endpoint is also used to calculate the issues endpoint
        buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://api.github.com/repos/Adamantcheese/Kuroba/releases/latest\""
        buildConfigField "String", "GITHUB_ENDPOINT", "\"https://github.com/Adamantcheese/Kuroba\""
        //this is for checking who's built what in debug logs
        buildConfigField "String", "SIGNATURE", "\"8952c098\""

        /**
         * ------------------------------------------------------------
         *
         *              DON'T CHANGE THESE AUTO-CALCULATIONS
         *              USED FOR VERSION CODE GENERATION
         *              USED FOR VERSION NAME GENERATION
         *              USED FOR AUTO UPDATER NAMING CONSISTENCY
         *
         * ------------------------------------------------------------
         */
        versionCode major * 10000 + minor * 100 + patch
        versionName "v" + major + "." + minor + "." + patch + "-dev"

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = manifestPlaceholders.get("appName").toString() + ".apk"
            }
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    // Needed for volley
    useLibrary 'org.apache.http.legacy'

    packagingOptions {
        exclude 'META-INF/*'
    }

    productFlavors {
        stable {
            dimension "default"
            applicationIdSuffix ""
            versionNameSuffix ""

            buildConfigField "boolean", "DEV_BUILD", "false"

            // These are manifest placeholders for the application name, icon location and file
            // provider authority (the file provider authority should differ for different flavors
            // otherwise the app will not work)
            manifestPlaceholders = [
                    appName              : "Kuroba",
                    iconLoc              : "@mipmap/ic_launcher",
                    fileProviderAuthority: "${applicationIdSuffix}.fileprovider"
            ]
        }
        dev {
            dimension "default"
            // Different app ids for different flavors so that the users are able to install both
            // of them without deleting anything
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"

            // To easily figure out whether the app uses development flavors or not
            buildConfigField "boolean", "DEV_BUILD", "true"

            // The same as in stable flavor
            manifestPlaceholders = [
                    appName              : "Kuroba${versionNameSuffix}",
                    iconLoc              : "@mipmap/ic_launcher",
                    fileProviderAuthority: "${applicationIdSuffix}.fileprovider"
            ]
        }
    }

    buildTypes {
        // manifestPlaceholders do not work here for some reason so here is a little hack.
        // We need to iterate each build variant and for each build variant we need to find it's
        // flavor then we need to extract the versionNameSuffix from the flavor and update the output
        // apk name with it.
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                Object flavor = getCurrentFlavor(variant.flavorName)
                if (flavor == null) {
                    throw new GradleException("Coudln't find flavor by variant.flavorName = ${variant.flavorName}")
                }

                outputFileName = "Kuroba${flavor.versionNameSuffix}.apk"
            }
        }

        release {
            /*
            If you want to sign releases without using "Generate Signed APK", make a file in app/keys.properties with the following content:
            keystoreFile=yourkey.store
            keystorePass=pass
            keyAlias=alias
            keyPass=pass
            */
            // Just select your build variant (debug/release) and do Build -> APK
            File f = file('keys.properties')
            if (f.exists()) {
                Properties props = new Properties()
                InputStream is = new FileInputStream(f)
                props.load(is)

                signingConfigs {
                    release {
                        storeFile file(props['keystoreFile'])
                        storePassword props['keystorePass']
                        keyAlias props['keyAlias']
                        keyPassword props['keyPass']
                    }
                }

                is.close()
                signingConfig signingConfigs.release
            }
            // Because we're always on dev, don't minify to make sure that crash logs are intelligible
            //You can turn this on if you wish
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            debuggable = false
        }
        debug {
            minifyEnabled false
            debuggable = true
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.collection:collection:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'
    implementation 'androidx.core:core:1.0.2'
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-common:2.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.slidingpanelayout:slidingpanelayout:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.google.android.exoplayer:exoplayer:2.10.4'

    implementation 'com.squareup.okhttp3:okhttp:4.1.0'

    implementation 'com.j256.ormlite:ormlite-core:5.1'
    implementation 'com.j256.ormlite:ormlite-android:5.1'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.17'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'org.nibor.autolink:autolink:0.10.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'me.xdrop:fuzzywuzzy:1.2.0'
    implementation 'org.codejargon.feather:feather:1.0'
    implementation 'com.vladsch.flexmark:flexmark:0.50.28'
    implementation 'com.vdurmont:emoji-java:5.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
}

//========================================================
// All of the below is being used by the upload_apk script
//========================================================

def getCurrentFlavor(String name) {
    Object resultFlavor = null

    android.productFlavors.all { flavor ->
        if (flavor.name == name) {
            resultFlavor = flavor
        }
    }

    return resultFlavor
}

def getLatestCommit(String branchName) {
    def stdout = new ByteArrayOutputStream()
    exec {
        def printArgs = "%H; %ad; %s"

        commandLine 'git', 'log', branchName, '-n 1', "--pretty=format:${printArgs}", "--date=iso8601-strict"
        standardOutput = stdout
    }

    return stdout.toString().trim()
}

def getLastCommitsFromCommitByHash(String branchName, String from) {
    def stdout = new ByteArrayOutputStream()
    exec {
        def printArgs = "%H; %ad; %s"

        commandLine 'git', 'log', branchName, "${from}..HEAD", "--pretty=format:${printArgs}", "--date=iso8601-strict"
        standardOutput = stdout
    }

    return stdout.toString().trim()
}

def checkBranchExists(String branchName) {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--verify', branchName
        standardOutput = stdout
    }

    return stdout.toString().trim()
}

task checkBranchExistsTask {
    doLast {
        println(checkBranchExists(branch_name))
    }
}

task getLatestCommitTask {
    doLast {
        println(getLatestCommit(branch_name))
    }
}

task getLastCommitsFromCommitByHashTask {
    doLast {
        println(getLastCommitsFromCommitByHash(branch_name, from))
    }
}

task getVersionCodeTask {
    doLast {
        println(project.android.defaultConfig.versionCode)
    }
}

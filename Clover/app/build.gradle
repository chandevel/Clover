apply plugin: 'com.android.application'

/**
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 24
    // update the travis config when changing this
    buildToolsVersion '24.0.3'

    defaultConfig {
        applicationId "org.floens.chan"
        minSdkVersion 15
        targetSdkVersion 24

        versionName getVersionName()
        versionCode 56
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    // Needed for volley
    useLibrary 'org.apache.http.legacy'

    /*
    If you want to sign releases, make a file in app/keys.properties with the following content:
    keystoreFile=yourkey.store
    keystorePass=pass
    keyAlias=alias
    keyPass=pass
    */

    File f = file('keys.properties')
    boolean doSign = f.exists();

    if (doSign) {
        Properties props = new Properties()
        InputStream is = new FileInputStream(f)
        props.load(is)

        signingConfigs {
            release {
                storeFile file(props['keystoreFile'])
                storePassword props['keystorePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }

        is.close()
    }

    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles 'proguard.cfg'
        }

        debug {
            versionNameSuffix " Debug"
//            minifyEnabled true
//            proguardFiles 'proguard.cfg'
        }
    }

    productFlavors {
        normal {
            applicationId = "org.floens.chan"
            resValue "string", "app_name", "Clover"
            resValue "string", "app_flavor_name", ""
        }
    }

    sourceSets {
        beta.java.srcDirs = ['src/release/java']
    }
}

dependencies {
    compile 'com.android.support:support-v13:24.2.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:support-annotations:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:customtabs:24.2.1'

    compile 'org.jsoup:jsoup:1.9.2'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.16'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'de.greenrobot:eventbus:2.4.0'
}

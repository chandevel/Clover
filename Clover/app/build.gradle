apply plugin: 'com.android.application'

if (project.sentryMappingUploadingEnabled) {
    apply plugin: 'io.sentry.android.gradle'

    sentry {
        // We'll do this manually because we only enable the sentry plugin on
        // the build server.
        autoProguardConfig false
    }
}

// Code style
apply plugin: 'checkstyle'
task('checkstyle', type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'
    classpath = files()
}


afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
}

/**
 * Gets the version name from the latest Git tag
 */
def getCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=8', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 29
    // update the travis config when changing this
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "org.floens.chan"
        minSdkVersion 17
        targetSdkVersion 29

        versionName "v3.0.2"
        // of the format XXYYZZ, where XX is major, YY is minor, ZZ is patch
        // (watch out for octal notation, never start with a 0)
        versionCode 30002
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    /*
    If you want to sign releases, make a file in app/keys.properties with the following content:
    keystoreFile=yourkey.store
    keystorePass=pass
    keyAlias=alias
    keyPass=pass
    */

    def hash = getCommitHash()

    File f = file('keys.properties')
    boolean doSign = f.exists()

    if (doSign) {
        Properties props = new Properties()
        InputStream is = new FileInputStream(f)
        props.load(is)

        signingConfigs {
            release {
                storeFile file(props['keystoreFile'])
                storePassword props['keystorePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }

        is.close()
    }

    defaultPublishConfig "default"
    flavorDimensions "default"
    productFlavors {
        // NOTE: the file provider authority names append to the package name.
        // When changing this also update the authority in the manifest files.

        // The app name refers to the name as displayed on the launcher.
        // the flavor name is appended to the name in the settings.
        "default" {
            dimension "default"
            resValue "string", "app_name", "Clover"
            resValue "string", "app_flavor_name", ""
            buildConfigField "String", "BUILD_HASH", "\"$hash\""
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://build.floens.org/api/update\""
            buildConfigField "String", "CRASH_REPORT_TOKEN", "\"https://d453201738b8469da2113a922c543a3b@sentry.io/1413946\""
        }

        dev {
            dimension "default"
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "Clover dev"
            resValue "string", "app_flavor_name", ""
            buildConfigField "String", "BUILD_HASH", "\"$hash\""
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://build.floens.org/api/update_dev\""
            buildConfigField "String", "CRASH_REPORT_TOKEN", "\"https://d453201738b8469da2113a922c543a3b@sentry.io/1413946\""

            versionNameSuffix "-" + hash
        }

        fdroid {
            dimension "default"
            resValue "string", "app_name", "Clover"
            resValue "string", "app_flavor_name", "F-Droid"
            buildConfigField "String", "BUILD_HASH", "\"$hash\""
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://chandevel.github.io/Clover/api/update\""
            buildConfigField "String", "CRASH_REPORT_TOKEN", "\"https://d453201738b8469da2113a922c543a3b@sentry.io/1413946\""
        }
    }

    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            buildConfigField "boolean", "DEVELOPER_MODE", "false"
        }

        debug {
//            minifyEnabled true
//            proguardFiles 'proguard.cfg'
            buildConfigField "boolean", "DEVELOPER_MODE", "true"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.7'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.11.7'

    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    //noinspection GradleDependency
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    //noinspection GradleDependency
    implementation 'com.j256.ormlite:ormlite-android:4.48'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.20'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    //noinspection GradleDependency
    implementation 'de.greenrobot:eventbus:2.4.0'
    implementation 'org.nibor.autolink:autolink:0.9.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'me.xdrop:fuzzywuzzy:1.3.1'
    implementation 'org.codejargon.feather:feather:1.0'
    implementation 'io.sentry:sentry-android:2.2.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.27.0'
}

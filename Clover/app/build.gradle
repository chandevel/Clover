apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23

        versionName "v2.1.0"
        versionCode 52
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    // Needed for volley
    useLibrary 'org.apache.http.legacy'

    /*
    If you want to sign releases, make a file in app/keys.properties with the following content:
    keystoreFile=yourkey.store
    keystorePass=pass
    keyAlias=alias
    keyPass=pass
    */

    File f = file('keys.properties')
    boolean doSign = f.exists();

    if (doSign) {
        Properties props = new Properties()
        InputStream is = new FileInputStream(f)
        props.load(is)

        signingConfigs {
            release {
                storeFile file(props['keystoreFile'])
                storePassword props['keystorePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }

        is.close()
    }

    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles 'proguard.cfg'
        }

        beta.initWith(buildTypes.release)
        beta {
            versionNameSuffix " Beta"
        }

        debug {
            versionNameSuffix " Debug"
//            minifyEnabled true
//            proguardFiles 'proguard.cfg'
        }
    }

    sourceSets {
        beta.java.srcDirs = ['src/release/java']
    }
}

dependencies {
    compile 'com.android.support:support-v13:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:support-annotations:23.1.0'
    compile 'com.android.support:design:23.1.0'

    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.10'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.4.1'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'de.greenrobot:eventbus:2.4.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    betaCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
